#DEFINITIONS:  -*-sh-*-
#
# ARTS control file for testing 1D propagation path calculations
# with different refractive index calculation methods (None, MicrowavesEarth, MicrowavesGeneral)
# for the different planets (particularly dependence on atmospheric composition)
#
# Jana Mendrok 2013-02-25

Arts2{

INCLUDE "general/general.arts"
INCLUDE "general/agendas.arts"

# Agenda for scalar gas absorption calculation
Copy(abs_xsec_agenda, abs_xsec_agenda__noCIA)

# Use sensor_pos/los to store each case
VectorCreate( za )
VectorCreate( ztan )
VectorSet( ztan, [50e3, 46e3, 34e3, 10e3, 5e3, 2.5e3] )
nelemGet( nelem, ztan )
MatrixSetConstant( sensor_pos, nelem, 1, 600e3 )
VectorSet( rte_pos2, [] )

IndexCreate( ilast )


Copy( ppath_agenda, ppath_agenda__FollowSensorLosPath )
IndexSet( stokes_dim, 1 )
AtmosphereSet1D
jacobianOff
cloudboxOff
NumericSet( ppath_lmax, 5e3 )

# A dummy frequency grid
VectorSet( f_grid, [10e9] )


IndexCreate( tpoutlev )
IndexSet( tpoutlev, 1 )
IndexCreate( outlev )
IndexSet( outlev, 0 )
NumericCreate( tanh1 )
NumericCreate( tanlat1 )
VectorCreate( tp1 )
NumericCreate( tanh2 )
NumericCreate( tanlat2 )
VectorCreate( tp2 )
NumericCreate( diff )
StringCreate( infostring )
Tensor3Create( vmr_field3 )
MatrixCreate( vmr_field2 )
MatrixCreate( t_field2 )
VectorCreate( t_field1 )
StringCreate( outfile )
StringCreate( str )
AgendaSet( forloop_agenda ){
  NumericSet(refr_index_air, 0)
  NumericSet(refr_index_air_group, 0)

  Tensor3ExtractFromTensor4(vmr_field3, vmr_field, forloop_index, "page")
  MatrixExtractFromTensor3(vmr_field2, vmr_field3, 0, "row")
  VectorExtractFromMatrix(rtp_vmr, vmr_field2, 0, "column")

  MatrixExtractFromTensor3(t_field2, t_field, forloop_index, "page")
  VectorExtractFromMatrix(t_field1, t_field2, 0, "row")
  Extract(rtp_temperature, t_field1, 0)

  Extract(rtp_pressure, p_grid, forloop_index)

  #Print(rtp_pressure, 0)
  #Print(rtp_temperature, 0)
  #Print(rtp_vmr, 0)
  refr_index_airMicrowavesGeneral(refr_index_air, refr_index_air_group,
              rtp_pressure, rtp_temperature, rtp_vmr, abs_species )
  #Print(refr_index_air, 0)

  StringSet(str, "refindexair")
  Append(str, outfile)
  WriteXMLIndexed("ascii", forloop_index, refr_index_air, str, 3)
  #WriteXMLIndexed("ascii", forloop_index, rtp_temperature, outfile, 3)
  #WriteXMLIndexed("ascii", forloop_index, rtp_pressure, outfile, 3)
}


#####
# CASE A: Earth
#####

# Main atmospheric contributors
#  When comparing effects of planetary composition, we don't want further
#  confusion by H2O. so we leave it out here.
abs_speciesSet( species=[
"C2H2",
"C2H6",
"CH3Cl",
"CH4",
"CO",
"CO2",
"COF2",
"ClO",
"H2CO",
"H2O",
"H2O2",
"HBr",
"HCN",
"HCl",
"HF",
"HI",
"HNO3",
"HOCl",
"N2",
"N2O",
"NH3",
"NO",
"NO2",
"O2",
"O3",
"OH",
"PH3",
"SF6",
"SO2"
])

refellipsoidEarth( refellipsoid, "Sphere" )
# convert (geometric) tangent altitudes to zenith angles at observer
VectorZtanToZa1D( za, sensor_pos, refellipsoid, atmosphere_dim, ztan )
Matrix1ColFromVector( sensor_los, za )

VectorNLogSpace( p_grid, 100, 1100e2, 0.1 )

AtmRawRead( basename = "planets/Earth/Fascod/tropical/tropical" )
#AtmRawRead( basename = "planets/Earth/Fascod/midlatitude-winter/midlatitude-winter" )
AtmFieldsCalc
Extract( z_surface, z_field, 0 )

atmfields_checkedCalc
atmgeom_checkedCalc
cloudbox_checkedCalc

nelemGet( ilast, p_grid )
IndexStepDown( ilast, ilast )
StringSet(outfile, ".Earth")
WriteXML("ascii", p_grid, "p_grid.Earth.xml")
WriteXML("ascii", t_field, "t_field.Earth.xml")
ForLoop( forloop_agenda, 0, ilast, 1  )
#####
# CASE B: Mars
#####

# Main atmospheric contributors
abs_speciesSet( species=[
"CH4",
"CO",
"CO2",
"H2",
"H2O",
"H2O2",
"H2S",
"HCl",
"N2",
"NO2",
"O",
"O2",
"O3",
"OCS",
"OH",
"SO2"
])

refellipsoidMars( refellipsoid, "Sphere" )
# convert (geometric) tangent altitudes to zenith angles at observer
VectorZtanToZa1D( za, sensor_pos, refellipsoid, atmosphere_dim, ztan )
Matrix1ColFromVector( sensor_los, za )

VectorNLogSpace( p_grid, 100, 766, 0.1 )

AtmRawRead( basename = "planets/Mars/MPS/Mars.Ls0.day.dust-medium/Mars.Ls0.day.dust-medium.sol-avg/Mars.Ls0.day.dust-medium.sol-avg" )
AtmFieldsCalc
Extract( z_surface, z_field, 0 )

atmfields_checkedCalc
atmgeom_checkedCalc
cloudbox_checkedCalc

nelemGet( ilast, p_grid )
IndexStepDown( ilast, ilast )
StringSet(outfile, ".Mars")
WriteXML("ascii", p_grid, "p_grid.Mars.xml")
WriteXML("ascii", t_field, "t_field.Mars.xml")
ForLoop( forloop_agenda, 0, ilast, 1  )


#####
# CASE C: Venus
#####

# Main atmospheric contributors
abs_speciesSet( species=[
"CO",
"CO2",
"H2O",
"H2SO4",
"HCl",
"HF",
"N2",
"NO",
"NO2",
"O",
"O2",
"O3",
"OCS",
"SO",
"SO2"
])

refellipsoidVenus( refellipsoid, "Sphere" )
# convert (geometric) tangent altitudes to zenith angles at observer
VectorZtanToZa1D( za, sensor_pos, refellipsoid, atmosphere_dim, ztan )
Matrix1ColFromVector( sensor_los, za )

VectorNLogSpace( p_grid, 100, 9.2e6, 0.1 )

AtmRawRead( basename = "planets/Venus/MPS/Venus.vira.day/Venus.vira.day" )
AtmFieldsCalc( vmr_zeropadding=1 )
Extract( z_surface, z_field, 0 )

atmfields_checkedCalc( bad_partition_functions_ok = 1 )
atmgeom_checkedCalc
cloudbox_checkedCalc

nelemGet( ilast, p_grid )
IndexStepDown( ilast, ilast )
StringSet(outfile, ".Venus")
WriteXML("ascii", p_grid, "p_grid.Venus.xml")
WriteXML("ascii", t_field, "t_field.Venus.xml")
ForLoop( forloop_agenda, 0, ilast, 1  )


#####
# CASE D: Jupiter
#####

# Main atmospheric contributors
#abs_speciesSet( species=["CO2","H2","He"] )
abs_speciesSet( species=[
"C2H2",
"C2H4",
"C2H6",
"C3H8",
"CH4",
"CO",
"CO2",
"H2",
"H2O",
"H2S",
"HCN",
"He",
"NH3",
"PH3"
])


refellipsoidJupiter( refellipsoid, "Sphere" )
# convert (geometric) tangent altitudes to zenith angles at observer
VectorZtanToZa1D( za, sensor_pos, refellipsoid, atmosphere_dim, ztan )
Matrix1ColFromVector( sensor_los, za )

# Pressure grid rougly matching 0 to 80 km.
VectorNLogSpace( p_grid, 100, 1.0e6, 0.1 )

AtmRawRead( basename = "planets/Jupiter/MPS/Jupiter.mean/Jupiter.mean" )
AtmFieldsCalc( vmr_zeropadding=1 )
Extract( z_surface, z_field, 0 )

atmfields_checkedCalc( bad_partition_functions_ok = 1 )
atmgeom_checkedCalc
cloudbox_checkedCalc

nelemGet( ilast, p_grid )
IndexStepDown( ilast, ilast )
StringSet(outfile, ".Jupiter")
WriteXML("ascii", p_grid, "p_grid.Jupiter.xml")
WriteXML("ascii", t_field, "t_field.Jupiter.xml")
ForLoop( forloop_agenda, 0, ilast, 1  )

}
